#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (location = 0) in vec2 inUv;

layout (location = 0) out vec4 outFragColor;

#include "uniform_layout.sh"
#include "utilities.sh"
#include "pbr_functions.sh"

const uint numSamples = 1024;

void main() 
{
	float NdotV = inUv.s;
	float roughness = inUv.t;

	vec3 V;
    V.x = sqrt(1.0 - NdotV * NdotV);
    V.y = 0.0;
    V.z = NdotV;

    float A = 0.0;
    float B = 0.0;

    vec3 N = vec3(0.0, 0.0, 1.0);

    for(uint samples = 0; samples < numSamples; samples++)
    {
        vec2 Xi = Hammersley(samples, numSamples);
        vec3 H  = ImportanceSampleGGX(Xi, N, roughness);
        vec3 L  = normalize(2.0 * dot(V, H) * H - V);

        float NdotL = max(L.z, 0.0);
        float NdotH = max(H.z, 0.0);
        float VdotH = max(dot(V, H), 0.0);

        if(NdotL > 0.0)
        {
            float G = GGX_V_Smith_HeightCorrelated(NdotV, NdotL, roughness);
            float G_Vis = (G * VdotH) / (NdotH * NdotV);
            float Fc = pow(1.0 - VdotH, 5.0);

            A += (1.0 - Fc) * G_Vis;
            B += Fc * G_Vis;
        }
    }

    A /= float(numSamples);
    B /= float(numSamples);

	outFragColor = vec4(vec2(A, B), 0.0, 1.0);
}